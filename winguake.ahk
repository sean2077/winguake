#Requires AutoHotkey v2.0
; Windows Quake - Quick Window Switcher
; AutoHotkey v2 Script
; Authorï¼šSean2077

VERSION := "1.3.0"
SCRIPT_NAME := "winguake(v" . VERSION . ")"

; ==================== å¤šè¯­è¨€æ”¯æŒ ====================
; æ£€æµ‹ç³»ç»Ÿè¯­è¨€å¹¶è®¾ç½®å¯¹åº”çš„è¯­è¨€åŒ…
SYSTEM_LANG := A_Language
IS_CHINESE := (SYSTEM_LANG = "0804" || SYSTEM_LANG = "0404" || SYSTEM_LANG = "0C04" || SYSTEM_LANG = "1004" || SYSTEM_LANG = "1404")

; è¯­è¨€åŒ…
Lang := {}

if (IS_CHINESE) {
    ; ä¸­æ–‡è¯­è¨€åŒ…
    Lang.SCRIPT_FULLNAME := "Windows Quake - å¿«é€Ÿçª—å£åˆ‡æ¢å™¨ (v" . VERSION . ")"
    Lang.CONFIG_EMPTY := "é…ç½®æ–‡ä»¶ä¸ºç©º"
    Lang.CONFIG_SUCCESS := "è¯»å–é…ç½®æ–‡ä»¶æˆåŠŸ ğŸ‰ğŸ‰ğŸ‰"
    Lang.CONFIG_FAILED := "è¯»å–é…ç½®æ–‡ä»¶å¤±è´¥ ğŸ’©ğŸ’©ğŸ’©"
    Lang.CONFIG_NOT_FOUND := "æœªæ‰¾åˆ°é…ç½®æ–‡ä»¶ï¼Œä½¿ç”¨é»˜è®¤é…ç½®"
    Lang.CONFIG_VALIDATION_PASSED := "æ‰€æœ‰é…ç½®éªŒè¯é€šè¿‡"
    Lang.CONFIG_VALIDATION_ISSUES := "é…ç½®éªŒè¯å‘ç°é—®é¢˜ï¼š`n`n"
    Lang.CONFIG_FILE_MISSING := "æœªæ‰¾åˆ°é…ç½®æ–‡ä»¶"
    Lang.CONFIG_FILE_CREATE_PROMPT := "`næ˜¯å¦è¦åˆ›å»ºæ–°çš„é…ç½®æ–‡ä»¶ï¼Ÿ"
    Lang.HOTKEY_REGISTER_FAILED := "çƒ­é”®æ³¨å†Œå¤±è´¥"
    Lang.APP_NOT_FOUND := "æœªæ‰¾åˆ°åº”ç”¨"
    Lang.APP_MINIMIZED := "å·²æœ€å°åŒ–"
    Lang.APP_ACTIVATED := "å·²æ¿€æ´»"
    Lang.APP_ALL_MINIMIZED := "æ‰€æœ‰çª—å£å·²æœ€å°åŒ–"
    Lang.APP_STARTING := "æ­£åœ¨å¯åŠ¨"
    Lang.APP_STARTING_MAXIMIZED := "æ­£åœ¨å¯åŠ¨ï¼ˆæœ€å¤§åŒ–ï¼‰"
    Lang.APP_MAXIMIZED := "å·²æœ€å¤§åŒ–"
    Lang.APP_MAXIMIZED_BACKUP := "å·²æœ€å¤§åŒ–ï¼ˆå¤‡ç”¨æ–¹æ³•ï¼‰"
    Lang.APP_MAXIMIZED_FAILED := "å¤‡ç”¨æœ€å¤§åŒ–å¤±è´¥"
    Lang.APP_ALREADY_MAXIMIZED := "å·²ç»æ˜¯æœ€å¤§åŒ–çŠ¶æ€"
    Lang.LAUNCH_FAILED := "å¯åŠ¨å¤±è´¥"
    Lang.LAUNCH_FAILED_MSG := "æ— æ³•å¯åŠ¨åº”ç”¨ç¨‹åºã€‚`nè¯·ç¡®ä¿åº”ç”¨ç¨‹åºå·²æ­£ç¡®å®‰è£…ã€‚"
    Lang.STATUS_TITLE := "åº”ç”¨çŠ¶æ€"
    Lang.STATUS_HEADER := "Windows Quake - åº”ç”¨ç¨‹åºçŠ¶æ€ï¼š`n`n"
    Lang.STATUS_WINDOWS := "ä¸ªçª—å£"
    Lang.STATUS_ACTIVE := "æ´»åŠ¨"
    Lang.STATUS_MINIMIZED := "æœ€å°åŒ–"
    Lang.STATUS_NOT_RUNNING := "æœªè¿è¡Œ"
    Lang.MENU_TOGGLE := "åˆ‡æ¢"
    Lang.MENU_SHOW_STATUS := "æ˜¾ç¤ºåº”ç”¨çŠ¶æ€"
    Lang.MENU_SHOW_CONFIG := "ç¼–è¾‘å½“å‰åº”ç”¨é…ç½®çŠ¶æ€"
    Lang.MENU_OPEN_DIR := "æ‰“å¼€è„šæœ¬å’Œé…ç½®æ–‡ä»¶ç›®å½•"
    Lang.MENU_OPEN_CONFIG := "æ‰“å¼€é…ç½®æ–‡ä»¶"
    Lang.MENU_HELP := "å¸®åŠ©"
    Lang.MENU_EXIT := "é€€å‡º"
    Lang.HELP_TITLE := "å¸®åŠ©"
    Lang.HELP_CURRENT_APPS := "`n`nå½“å‰ç®¡ç†çš„åº”ç”¨ç¨‹åºï¼š`n"
    Lang.HELP_HOTKEY_DESC := "`n`nçƒ­é”®è¯´æ˜ï¼š`n    çƒ­é”®        - åˆ‡æ¢å¯¹åº”åº”ç”¨ç¨‹åºçš„æ˜¾ç¤º/éšè—/å¯åŠ¨"
    Lang.HELP_FUNCTION_DESC := "`n`nåŠŸèƒ½è¯´æ˜ï¼š`n    â€¢ é¦–æ¬¡æŒ‰ä¸‹çƒ­é”®å°†å¯åŠ¨å¯¹åº”çš„åº”ç”¨ç¨‹åº`n    â€¢ å•çª—å£ï¼šæ´»åŠ¨æ—¶æœ€å°åŒ–ï¼Œéæ´»åŠ¨æ—¶æ¿€æ´»`n    â€¢ å¤šçª—å£ï¼šæä¾›ä¸¤ç§å¾ªç¯æ¨¡å¼ï¼ˆå¯é€šè¿‡ cycleContinuous é€‰é¡¹é…ç½®ï¼‰`n    â€¢ æ˜¾ç¤ºçª—å£æ ‡é¢˜å’Œç¼–å·ï¼Œä¾¿äºè¯†åˆ«"
    Lang.HELP_CYCLE_LOGIC := "`n`nå¤šçª—å£å¾ªç¯é€»è¾‘ï¼š`n    1. é¦–æ¬¡æŒ‰ä¸‹ï¼šæ¿€æ´»ç¬¬ä¸€ä¸ªçª—å£`n    2. ç»§ç»­æŒ‰ä¸‹ï¼šä¾æ¬¡æ¿€æ´»åç»­çª—å£`n    3. æœ€åçª—å£è¡Œä¸ºï¼ˆå–å†³äº cycleContinuous è®¾ç½®ï¼‰ï¼š`n       - cycleContinuous=trueï¼ˆé»˜è®¤ï¼‰ï¼šè·³å›ç¬¬ä¸€ä¸ªçª—å£å¹¶ç»§ç»­å¾ªç¯`n       - cycleContinuous=falseï¼šæœ€å°åŒ–æ‰€æœ‰çª—å£`n    4. æ‰€æœ‰çª—å£æœ€å°åŒ–æ—¶ï¼šå†æ¬¡æŒ‰ä¸‹ä»ç¬¬ä¸€ä¸ªçª—å£å¼€å§‹"
    Lang.HELP_CONFIG_OPTIONS := "`n`né…ç½®é€‰é¡¹ï¼š`n    â€¢ cycleContinuous: true/false - æ§åˆ¶å¤šçª—å£å¾ªç¯è¡Œä¸º`n    â€¢ maximize: true/false - æ˜¯å¦åœ¨å¯åŠ¨æ—¶æœ€å¤§åŒ–çª—å£`n    â€¢ disable: true/false - æ˜¯å¦ç¦ç”¨æ­¤åº”ç”¨ç¨‹åº"
    Lang.HELP_ADD_APPS := "`n`næ·»åŠ æ–°åº”ç”¨ç¨‹åºï¼š`n    åªéœ€åœ¨è„šæœ¬å¼€å¤´çš„ Apps éƒ¨åˆ†æ·»åŠ æ–°çš„åº”ç”¨ç¨‹åºé…ç½®å³å¯ï¼`n    æ‚¨ä¹Ÿå¯ä»¥ä¿®æ”¹é…ç½®æ–‡ä»¶ï¼Œé€šè¿‡å³é”®å•å‡»`"æ‰“å¼€é…ç½®æ–‡ä»¶`"æ‰¾åˆ°é…ç½®æ–‡ä»¶ä½ç½®ã€‚`n`n    å³é”®å•å‡»ç³»ç»Ÿæ‰˜ç›˜å›¾æ ‡å¯è®¿é—®æ›´å¤šåŠŸèƒ½ã€‚"
    Lang.CONFIG_TITLE := "å½“å‰é…ç½®"
    Lang.CONFIG_HEADER := "å½“å‰åº”ç”¨é…ç½®ï¼ˆæœ‰æ•ˆéƒ¨åˆ†ï¼‰ï¼š`n`n"
    Lang.CONFIG_HOTKEY := "  çƒ­é”®ï¼š"
    Lang.CONFIG_PROCESS := "  è¿›ç¨‹åï¼š"
    Lang.CONFIG_LAUNCH_CMD := "  å¯åŠ¨å‘½ä»¤ï¼š"
    Lang.CONFIG_DISPLAY_NAME := "  æ˜¾ç¤ºåç§°ï¼š"
    Lang.CONFIG_LAUNCH_PATHS := "  å¯åŠ¨è·¯å¾„ï¼š"
    Lang.CONFIG_DISABLED := "  å·²ç¦ç”¨ï¼š"
    Lang.CONFIG_MAXIMIZE := "  å¯åŠ¨æ—¶æœ€å¤§åŒ–ï¼š"
    Lang.CONFIG_CYCLE := "  æŒç»­å¾ªç¯ï¼š"
    Lang.YES := "æ˜¯"
    Lang.NO := "å¦"
    Lang.STARTUP_TITLE := "å¯åŠ¨é€šçŸ¥"
    Lang.STARTUP_RUNNING := "æ­£åœ¨è¿è¡Œ"
    Lang.VALIDATION_TITLE := "é…ç½®éªŒè¯"
    Lang.UPDATED := "å·²æ›´æ–°"
    Lang.APPLICATIONS := "ä¸ªåº”ç”¨ç¨‹åº"
    Lang.NO_UPDATES := "æœªæ‰¾åˆ°æœ‰æ•ˆçš„é…ç½®æ›´æ–°"
    ; é…ç½®ç¼–è¾‘å™¨ç›¸å…³æ–‡æœ¬
    Lang.CONFIG_EDITOR_HELP := "åœ¨ä¸‹æ–¹ç¼–è¾‘é…ç½®ï¼Œç„¶åç‚¹å‡» 'ä¿å­˜å¹¶åº”ç”¨' æ¥æ›´æ–°è®¾ç½®ã€‚é…ç½®é‡‡ç”¨INIæ ¼å¼ã€‚"
    Lang.CONFIG_SAVE_APPLY := "ä¿å­˜å¹¶åº”ç”¨"
    Lang.CONFIG_RESET := "é‡ç½®"
    Lang.CONFIG_FORMAT_DESC := "é…ç½®æ–‡ä»¶æ ¼å¼è¯´æ˜ï¼š"
    Lang.CONFIG_SECTION_DESC := "æ¯ä¸ªåº”ç”¨ç¨‹åºä¸€ä¸ªæ®µè½ [AppName]"
    Lang.CONFIG_OPTIONS_DESC := "æ”¯æŒçš„é…ç½®é¡¹ï¼šhotkey, exe, launchCmd, name, launchPaths, maximize, cycleContinuous, disable"
    Lang.CONFIG_PATHS_DESC := "launchPaths ç”¨ | åˆ†éš”å¤šä¸ªè·¯å¾„"
    Lang.CONFIG_SAVE_SUCCESS := "é…ç½®å·²ä¿å­˜å¹¶åº”ç”¨æˆåŠŸï¼"
    Lang.CONFIG_VALIDATION_FAILED := "é…ç½®éªŒè¯å¤±è´¥ï¼Œè¯·æ£€æŸ¥é…ç½®æ ¼å¼ã€‚"
    Lang.CONFIG_SAVE_FAILED := "ä¿å­˜é…ç½®æ–‡ä»¶å¤±è´¥ï¼š"
    Lang.CONFIG_RESET_CONFIRM := "ç¡®å®šè¦é‡ç½®åˆ°å½“å‰é…ç½®å—ï¼Ÿæ‰€æœ‰æœªä¿å­˜çš„æ›´æ”¹å°†ä¸¢å¤±ã€‚"
} else {
    ; è‹±æ–‡è¯­è¨€åŒ…ï¼ˆé»˜è®¤ï¼‰
    Lang.SCRIPT_FULLNAME := "Windows Quake - Quick Window Switcher (v" . VERSION . ")"
    Lang.CONFIG_EMPTY := "configuration is empty"
    Lang.CONFIG_SUCCESS := "Read config file successful ğŸ‰ğŸ‰ğŸ‰"
    Lang.CONFIG_FAILED := "Read config file failed ğŸ’©ğŸ’©ğŸ’©"
    Lang.CONFIG_NOT_FOUND := "Config file not found, use default"
    Lang.CONFIG_VALIDATION_PASSED := "All configuration validation passed"
    Lang.CONFIG_VALIDATION_ISSUES := "Configuration validation found issues:`n`n"
    Lang.CONFIG_FILE_MISSING := "Configuration file not found"
    Lang.CONFIG_FILE_CREATE_PROMPT := "`nDo you want to create a new one?"
    Lang.HOTKEY_REGISTER_FAILED := "Failed to register hotkey"
    Lang.APP_NOT_FOUND := "Not found"
    Lang.APP_MINIMIZED := "is minimized"
    Lang.APP_ACTIVATED := "is activated"
    Lang.APP_ALL_MINIMIZED := "all windows are minimized"
    Lang.APP_STARTING := "Starting"
    Lang.APP_STARTING_MAXIMIZED := "Starting (maximized)"
    Lang.APP_MAXIMIZED := "maximized"
    Lang.APP_MAXIMIZED_BACKUP := "maximized (backup method)"
    Lang.APP_MAXIMIZED_FAILED := "backup maximize failed"
    Lang.APP_ALREADY_MAXIMIZED := "already maximized"
    Lang.LAUNCH_FAILED := "Launch Failed"
    Lang.LAUNCH_FAILED_MSG := "Unable to start application.`nPlease ensure the application is installed correctly."
    Lang.STATUS_TITLE := "Application Status"
    Lang.STATUS_HEADER := "Windows Quake - application status:`n`n"
    Lang.STATUS_WINDOWS := "windows"
    Lang.STATUS_ACTIVE := "Active"
    Lang.STATUS_MINIMIZED := "Minimized"
    Lang.STATUS_NOT_RUNNING := "Not Running"
    Lang.MENU_TOGGLE := "Toggle"
    Lang.MENU_SHOW_STATUS := "Show app status"
    Lang.MENU_SHOW_CONFIG := "Edit current app config status"
    Lang.MENU_OPEN_DIR := "Open directory of script and config file"
    Lang.MENU_OPEN_CONFIG := "Open configuration file"
    Lang.MENU_HELP := "Help"
    Lang.MENU_EXIT := "Exit"
    Lang.HELP_TITLE := "Help"
    Lang.HELP_CURRENT_APPS := "`n`nCurrent managed applications:`n"
    Lang.HELP_HOTKEY_DESC := "`n`nHotkey Description:`n    Hotkey        - Toggle corresponding application display/hide/launch"
    Lang.HELP_FUNCTION_DESC := "`n`nFunction Description:`n    â€¢ The first press of the hotkey will start the corresponding application`n    â€¢ Single window: Minimize when active, activate when inactive`n    â€¢ Multi-window: Two cycle modes available (configurable via cycleContinuous option)`n    â€¢ Display window titles and numbers for easy identification"
    Lang.HELP_CYCLE_LOGIC := "`n`nMulti-window cycle logic:`n    1. First press: Activate the first window`n    2. Continue pressing: Activate subsequent windows in turn`n    3. Last window behavior (depends on cycleContinuous setting):`n       - cycleContinuous=true (default): Jump back to first window and continue cycling`n       - cycleContinuous=false: Minimize all windows`n    4. When all windows minimized: Press again to start from the first window"
    Lang.HELP_CONFIG_OPTIONS := "`n`nConfiguration options:`n    â€¢ cycleContinuous: true/false - Controls multi-window cycling behavior`n    â€¢ maximize: true/false - Whether to maximize window on startup`n    â€¢ disable: true/false - Whether to disable this application"
    Lang.HELP_ADD_APPS := "`n`nAdding new applications:`n    Simply add new application configurations to the Apps section at the beginning of the script!`n    You can also modify configuration file which can be located by right-clicking the `"Open Configuration file`".`n`n    Right-clicking the system tray icon provides access to more features."
    Lang.CONFIG_TITLE := "Current Configuration"
    Lang.CONFIG_HEADER := "Current application configuration (valid parts):`n`n"
    Lang.CONFIG_HOTKEY := "  Hotkey: "
    Lang.CONFIG_PROCESS := "  Process Name: "
    Lang.CONFIG_LAUNCH_CMD := "  Launch Command: "
    Lang.CONFIG_DISPLAY_NAME := "  Display Name: "
    Lang.CONFIG_LAUNCH_PATHS := "  Launch Paths: "
    Lang.CONFIG_DISABLED := "  Disabled: "
    Lang.CONFIG_MAXIMIZE := "  Maximize on Start: "
    Lang.CONFIG_CYCLE := "  Cycle Continuous: "
    Lang.YES := "Yes"
    Lang.NO := "No"
    Lang.STARTUP_TITLE := "Startup Notification"
    Lang.STARTUP_RUNNING := "is running"
    Lang.VALIDATION_TITLE := "Configuration Validation"
    Lang.UPDATED := "Updated: "
    Lang.APPLICATIONS := " applications"
    Lang.NO_UPDATES := "No valid configuration updates found"
    ; é…ç½®ç¼–è¾‘å™¨ç›¸å…³æ–‡æœ¬
    Lang.CONFIG_EDITOR_HELP := "Edit the configuration below, then click 'Save & Apply' to update settings. Configuration uses INI format."
    Lang.CONFIG_SAVE_APPLY := "Save & Apply"
    Lang.CONFIG_RESET := "Reset"
    Lang.CONFIG_FORMAT_DESC := "Configuration Format:"
    Lang.CONFIG_SECTION_DESC := "Each application has a section [AppName]"
    Lang.CONFIG_OPTIONS_DESC := "Supported options: hotkey, exe, launchCmd, name, launchPaths, maximize, cycleContinuous, disable"
    Lang.CONFIG_PATHS_DESC := "launchPaths use | to separate multiple paths"
    Lang.CONFIG_SAVE_SUCCESS := "Configuration saved and applied successfully!"
    Lang.CONFIG_VALIDATION_FAILED := "Configuration validation failed, please check the format."
    Lang.CONFIG_SAVE_FAILED := "Failed to save configuration file:"
    Lang.CONFIG_RESET_CONFIRM := "Are you sure you want to reset to current configuration? All unsaved changes will be lost."
}

; è®¾ç½®è„šæœ¬ä¸ºå•å®ä¾‹è¿è¡Œ
#SingleInstance Force

; è®¾ç½®æ‰˜ç›˜å›¾æ ‡æç¤º
A_IconTip := SCRIPT_NAME

; ==================== åº”ç”¨é…ç½® ====================
; ğŸ¯ åªéœ€è¦åœ¨è¿™é‡Œæ·»åŠ /ä¿®æ”¹åº”ç”¨é…ç½®å³å¯ï¼
; æ ¼å¼ï¼šé”®å => { é…ç½®é¡¹ }

Apps := Map()

; Windows Terminal é…ç½®
Apps["Terminal"] := {
    hotkey: "F3",                    ; å¿«æ·é”®
    exe: "WindowsTerminal.exe",      ; è¿›ç¨‹å
    launchCmd: "wt.exe",             ; å¯åŠ¨å‘½ä»¤
    launchPaths: [                   ; å¯åŠ¨è·¯å¾„åˆ—è¡¨ï¼ˆæŒ‰ä¼˜å…ˆçº§ï¼‰
        "wt.exe",
        "C:\Users\" . A_UserName . "\AppData\Local\Microsoft\WindowsApps\wt.exe"
    ],
    name: "Windows Terminal",        ; æ˜¾ç¤ºåç§°
    maximize: true,                  ; å¯åŠ¨æ—¶æœ€å¤§åŒ–
    cycleContinuous: true            ; å¤šçª—å£å¾ªç¯æ¨¡å¼ï¼štrue=æŒç»­å¾ªç¯ä¸æœ€å°åŒ–ï¼Œfalse=å¾ªç¯åˆ°æœ€åçª—å£æ—¶æœ€å°åŒ–æ‰€æœ‰çª—å£
}


Apps["VSCode"] := {
    hotkey: "F4",
    exe: "Code.exe",
    launchCmd: "code",
    launchPaths: [
        "code",
        "C:\Users\" . A_UserName . "\AppData\Local\Programs\Microsoft VS Code\Code.exe"
    ],
    name: "Visual Studio Code",
    maximize: true,  ; é»˜è®¤å¯åŠ¨æ—¶æœ€å¤§åŒ–
    cycleContinuous: true  ; å¤šçª—å£å¾ªç¯æ¨¡å¼ï¼šæŒç»­å¾ªç¯ä¸æœ€å°åŒ–
}

Apps["Chrome"] := {
    hotkey: "F5",
    exe: "chrome.exe",
    launchCmd: "chrome",
    launchPaths: [
        "chrome",
        "C:\Program Files\Google\Chrome\Application\chrome.exe",
        "C:\Program Files (x86)\Google\Chrome\Application\chrome.exe"
    ],
    name: "Google Chrome",
    maximize: true,  ; é»˜è®¤å¯åŠ¨æ—¶æœ€å¤§åŒ–
    cycleContinuous: true  ; å¤šçª—å£å¾ªç¯æ¨¡å¼ï¼šæŒç»­å¾ªç¯ä¸æœ€å°åŒ–
}

; ==================== è¯»å–é…ç½®æ–‡ä»¶ ====================

; è½¬æ¢å­—ç¬¦ä¸²ä¸ºå¸ƒå°”å€¼
ConvertToBool(value) {
    ; å»é™¤å‰åç©ºæ ¼å¹¶è½¬ä¸ºå°å†™
    cleanValue := Trim(StrLower(value))

    ; æ£€æŸ¥çœŸå€¼
    if (cleanValue = "true" || cleanValue = "yes" || cleanValue = "1" || cleanValue = "on") {
        return true
    }

    ; æ£€æŸ¥å‡å€¼
    if (cleanValue = "false" || cleanValue = "no" || cleanValue = "0" || cleanValue = "off" || cleanValue = "") {
        return false
    }

    ; é»˜è®¤è¿”å›å‡å€¼
    return false
}

; è·å–è„šæœ¬çœŸå®è·¯å¾„ï¼ˆå¤„ç†å¿«æ·æ–¹å¼ï¼‰
GetScriptRealPath() {
    scriptPath := A_ScriptFullPath

    ; æ£€æŸ¥æ˜¯å¦æ˜¯å¿«æ·æ–¹å¼(.lnk)
    if (SubStr(scriptPath, -3) = ".lnk") {
        ; åˆ›å»º Shell å¯¹è±¡è§£æå¿«æ·æ–¹å¼
        shell := ComObject("WScript.Shell")
        shortcut := shell.CreateShortcut(scriptPath)
        scriptPath := shortcut.TargetPath
    }

    return scriptPath
}

; è·å–é…ç½®æ–‡ä»¶è·¯å¾„
GetConfigFilePath() {
    realScriptPath := GetScriptRealPath()

    ; è·å–è„šæœ¬ç›®å½•å’Œæ–‡ä»¶åï¼ˆä¸å«æ‰©å±•åï¼‰
    scriptDir := RegExReplace(realScriptPath, "\\[^\\]+$")
    scriptNameNoExt := "winguake"  ; é»˜è®¤é…ç½®æ–‡ä»¶å

    ; æ„å»ºé…ç½®æ–‡ä»¶è·¯å¾„
    configPath := scriptDir . "\" . scriptNameNoExt . ".ini"

    return configPath
}


LoadConfig(configFile) {
    ; è·å–é…ç½®æ–‡ä»¶ä¸­çš„æ‰€æœ‰ Section
    allSections := IniRead(configFile)
    if (allSections = "") {
        ToolTip(Lang.CONFIG_EMPTY, , , 1)
        SetTimer(() => ToolTip("", , , 1), -2000)
        return true
    }

    sectionArray := StrSplit(allSections, "`n")
    updatedApps := []

    ; éå†æ¯ä¸ª Section
    for index, sectionName in sectionArray {
        ; è·³è¿‡ç©ºçš„ Section åç§°
        if (sectionName = "")
            continue

        ; æ£€æŸ¥æ˜¯å¦æ˜¯å·²çŸ¥çš„ App
        if (Apps.Has(sectionName)) {
            ; è·å–å½“å‰ Section çš„æ‰€æœ‰é”®
            allKeys := IniRead(configFile, sectionName)
            if (allKeys = "")
                continue

            keyArray := StrSplit(allKeys, "`n")

            ; éå†æ¯ä¸ªé”®å€¼å¯¹
            for keyIndex, keyValuePair in keyArray {
                if (keyValuePair = "")
                    continue

                ; è§£æ key=value æ ¼å¼
                equalPos := InStr(keyValuePair, "=")
                if (equalPos = 0)
                    continue

                keyName := SubStr(keyValuePair, 1, equalPos - 1)
                value := SubStr(keyValuePair, equalPos + 1)

                ; è·³è¿‡ç©ºé”®åæˆ–ç©ºå€¼
                if (keyName = "" || value = "")
                    continue

                ; æ ¹æ®é”®åæ›´æ–°å¯¹åº”çš„é…ç½®
                switch keyName {
                    case "launchPaths":
                        ; å¤„ç†æ•°ç»„ç±»å‹çš„é…ç½®
                        ; æ ¼å¼ï¼špath1|path2|path3
                        if (InStr(value, "|")) {
                            Apps[sectionName].launchPaths := StrSplit(value, "|")
                        } else {
                            Apps[sectionName].launchPaths := [value]
                        }

                    case "maximize", "cycleContinuous", "disable":
                        ; å¤„ç†å¸ƒå°”ç±»å‹çš„é…ç½®
                        Apps[sectionName].%keyName% := ConvertToBool(value)

                    default:
                        Apps[sectionName].%keyName% := value
                }
            }

            updatedApps.Push(sectionName)
        } else {
            ; å¦‚æœæ˜¯æ–°çš„ Appï¼Œåˆ›å»ºæ–°çš„é…ç½®
            newApp := {}

            ; è·å–å½“å‰ Section çš„æ‰€æœ‰é”®
            allKeys := IniRead(configFile, sectionName)
            if (allKeys = "")
                continue

            keyArray := StrSplit(allKeys, "`n")

            ; éå†æ¯ä¸ªé”®å€¼å¯¹
            for keyIndex, keyValuePair in keyArray {
                if (keyValuePair = "")
                    continue

                ; è§£æ key=value æ ¼å¼
                equalPos := InStr(keyValuePair, "=")
                if (equalPos = 0)
                    continue

                keyName := SubStr(keyValuePair, 1, equalPos - 1)
                value := SubStr(keyValuePair, equalPos + 1)

                ; è·³è¿‡ç©ºé”®åæˆ–ç©ºå€¼
                if (keyName = "" || value = "")
                    continue

                ; æ ¹æ®é”®åè®¾ç½®é…ç½®
                switch keyName {
                    case "launchPaths":
                        ; å¤„ç†æ•°ç»„ç±»å‹çš„é…ç½®
                        if (InStr(value, "|")) {
                            newApp.launchPaths := StrSplit(value, "|")
                        } else {
                            newApp.launchPaths := [value]
                        }

                    case "maximize", "cycleContinuous", "disable":
                        ; å¤„ç†å¸ƒå°”ç±»å‹çš„é…ç½®
                        newApp.%keyName% := ConvertToBool(value)

                    default:
                        newApp.%keyName% := value
                }
            }

            ; ä¸ºæ–° App è®¾ç½®é»˜è®¤å€¼
            if (!newApp.HasOwnProp("hotkey"))
                newApp.hotkey := ""
            if (!newApp.HasOwnProp("exe"))
                newApp.exe := ""
            if (!newApp.HasOwnProp("launchCmd"))
                newApp.launchCmd := ""
            if (!newApp.HasOwnProp("name"))
                newApp.name := sectionName
            if (!newApp.HasOwnProp("launchPaths"))
                newApp.launchPaths := []
            if (!newApp.HasOwnProp("maximize"))
                newApp.maximize := false
            if (!newApp.HasOwnProp("cycleContinuous"))
                newApp.cycleContinuous := true
            if (!newApp.HasOwnProp("disable"))
                newApp.disable := false

            Apps[sectionName] := newApp
            updatedApps.Push(sectionName . " (New)")
        }
    }

    ; æ˜¾ç¤ºæ›´æ–°ç»“æœ
    if (updatedApps.Length > 0) {
        message := Lang.UPDATED . updatedApps.Length . Lang.APPLICATIONS . "`n"
        for index, appName in updatedApps {
            message .= "- " . appName . "`n"
        }
        ToolTip(message, , , 1)
        SetTimer(() => ToolTip("", , , 1), -3000)
    } else {
        ToolTip(Lang.NO_UPDATES, , , 1)
        SetTimer(() => ToolTip("", , , 1), -2000)
    }

    return ValidateAllConfigs()
}


; è¾…åŠ©å‡½æ•°ï¼šéªŒè¯é…ç½®å®Œæ•´æ€§
ValidateAppConfig(appName, appConfig) {
    issues := []
    ; æ£€æŸ¥æ˜¯å¦è¢«ç¦ç”¨ï¼Œè‹¥è¢«ç¦ç”¨åˆ™è·³è¿‡éªŒè¯
    if (IsDisabled(appConfig))
        return issues

    ; æ£€æŸ¥çœŸæ­£å¿…éœ€çš„å­—æ®µï¼ˆå¯åŠ¨åº”ç”¨çš„æœ€å°è¦æ±‚ï¼‰
    if (!appConfig.HasOwnProp("hotkey") || appConfig.hotkey = "")
        issues.Push("missing hotkey configuration: hotkey")

    if (!appConfig.HasOwnProp("exe") || appConfig.exe = "")
        issues.Push("missing process name configuration: exe")

    ; launchCmd æˆ– launchPaths è‡³å°‘éœ€è¦ä¸€ä¸ª
    hasLaunchCmd := appConfig.HasOwnProp("launchCmd") && appConfig.launchCmd != ""
    hasLaunchPaths := appConfig.HasOwnProp("launchPaths") && appConfig.launchPaths.Length > 0

    if (!hasLaunchCmd && !hasLaunchPaths)
        issues.Push("missing launch method: need either launchCmd or launchPaths")

    ; name å¦‚æœä¸ºç©ºï¼Œè®¾ç½®é»˜è®¤å€¼è€Œä¸æ˜¯æŠ¥é”™
    if (!appConfig.HasOwnProp("name") || appConfig.name = "")
        appConfig.name := appName

    return issues
}

; è¾…åŠ©å‡½æ•°ï¼šéªŒè¯æ‰€æœ‰åº”ç”¨é…ç½®
ValidateAllConfigs() {
    allIssues := Map()
    registeredHotkeys := Map()

    for appName, appConfig in Apps {
        issues := ValidateAppConfig(appName, appConfig)
        if (issues.Length > 0) {
            allIssues[appName] := issues
            Apps.Delete(appName)  ; åˆ é™¤æœ‰é—®é¢˜çš„åº”ç”¨é…ç½®
        } else {
            ; è®°å½•æœ‰æ•ˆçš„åº”ç”¨
            ; è·³è¿‡ç¦ç”¨çš„åº”ç”¨é…ç½®
            if (IsDisabled(appConfig)) {
                continue
            }
            if registeredHotkeys.Has(appConfig.hotkey) {
                if !allIssues.Has("HotKey Conflict!") {
                    allIssues["HotKey Conflict!"] := []
                }
                allIssues["HotKey Conflict!"].Push(appName . "'s hotkey '" . appConfig.hotkey . "' is already in use by " . registeredHotkeys[appConfig.hotkey])
            } else {
                registeredHotkeys[appConfig.hotkey] := appName
            }
        }
    }

    if (allIssues.Count > 0) {
        message := Lang.CONFIG_VALIDATION_ISSUES
        for appName, issues in allIssues {
            message .= "[" . appName . "]`n"
            for index, issue in issues {
                message .= "  - " . issue . "`n"
            }
            message .= "`n"
        }
        MsgBox(message, Lang.VALIDATION_TITLE . " - " . SCRIPT_NAME, "T15")
        ; ä»…å­˜åœ¨çƒ­é”®å†²çªæˆ–æœ‰æ•ˆåº”ç”¨é…ç½®ä¸º0çš„æƒ…å†µè®¤ä¸ºé…ç½®å¤±è´¥
        if allIssues.Has("HotKey Conflict!") || Apps.Count = 0
            return false
    }


    ToolTip(Lang.CONFIG_VALIDATION_PASSED, , , 1)
    SetTimer(() => ToolTip("", , , 1), -2000)
    return true

}

; è¾…åŠ©å‡½æ•°ï¼šæ‰“å¼€è„šæœ¬çœŸå®è·¯å¾„
OpenScriptDirectory() {
    local dir
    SplitPath(configFile, , &dir)
    Run(dir)
}

OpenConfigFile() {
    ; åˆ¤æ–­æ˜¯å¦å­˜åœ¨é…ç½®æ–‡ä»¶
    if !FileExist(configFile) {
        ; å¦‚æœé…ç½®æ–‡ä»¶ä¸å­˜åœ¨ï¼Œæç¤ºç”¨æˆ·æ˜¯å¦åˆ›å»º
        MsgBoxResult := MsgBox(Lang.CONFIG_FILE_MISSING . ": " . configFile . Lang.CONFIG_FILE_CREATE_PROMPT, Lang.CONFIG_FILE_MISSING . " - " . SCRIPT_NAME, 4)
        if (MsgBoxResult = "No") {
            return  ; ç”¨æˆ·é€‰æ‹©ä¸åˆ›å»ºï¼Œç›´æ¥è¿”å›
        }
        ; åˆ›å»ºæ–°çš„é…ç½®æ–‡ä»¶
        FileAppend("", configFile)
    }
    Run(configFile)
}


; è¾…åŠ©å‡½æ•°ï¼šæ˜¾ç¤ºå½“å‰é…ç½®
ShowCurrentConfig() {
    ; ä½¿ç”¨å¯ç¼–è¾‘çš„é…ç½®çª—å£
    ShowConfigEditor()
}

; æ˜¾ç¤ºé…ç½®ç¼–è¾‘å™¨çª—å£
ShowConfigEditor() {
    ; ç”Ÿæˆå½“å‰é…ç½®çš„INIæ ¼å¼æ–‡æœ¬
    configText := GenerateConfigText()

    ; åˆ›å»ºGUIçª—å£
    configGui := Gui("+Resize +MinSize500x400", Lang.CONFIG_TITLE . " - " . SCRIPT_NAME)
    configGui.MarginX := 10
    configGui.MarginY := 10

    ; æ·»åŠ è¯´æ˜æ–‡æœ¬
    infoCtrl := configGui.Add("Text", "w580 h30 +Wrap", Lang.CONFIG_EDITOR_HELP)
    infoCtrl.SetFont("s9")

    ; æ·»åŠ ç¼–è¾‘æ§ä»¶ï¼ˆå¯ç¼–è¾‘ï¼Œæ”¯æŒæ»šåŠ¨ï¼‰
    editCtrl := configGui.Add("Edit", "VScroll HScroll w580 h300", configText)
    editCtrl.SetFont("s9", "Consolas")  ; ä½¿ç”¨ç­‰å®½å­—ä½“ä¾¿äºç¼–è¾‘

    ; æ·»åŠ æŒ‰é’®è¡Œ
    buttonY := 350

    ; ä¿å­˜å¹¶åº”ç”¨æŒ‰é’®
    saveBtn := configGui.Add("Button", "x10 y" . buttonY . " w120 h30", Lang.CONFIG_SAVE_APPLY)
    saveBtn.OnEvent("Click", (*) => SaveConfigFromEditor(editCtrl, configGui))

    ; é‡ç½®æŒ‰é’®
    resetBtn := configGui.Add("Button", "x140 y" . buttonY . " w80 h30", Lang.CONFIG_RESET)
    resetBtn.OnEvent("Click", (*) => ResetConfigEditor(editCtrl))

    ; å…³é—­æŒ‰é’®
    closeBtn := configGui.Add("Button", "x480 y" . buttonY . " w100 h30", Lang.MENU_EXIT)
    closeBtn.OnEvent("Click", (*) => configGui.Destroy())

    ; è®¾ç½®çª—å£äº‹ä»¶
    configGui.OnEvent("Close", (*) => configGui.Destroy())
    configGui.OnEvent("Escape", (*) => configGui.Destroy())

    ; æ˜¾ç¤ºçª—å£
    configGui.Show("w600 h400")

    ; çª—å£å¤§å°è°ƒæ•´äº‹ä»¶
    configGui.OnEvent("Size", ConfigEditor_Size)

    ConfigEditor_Size(GuiObj, MinMax, Width, Height) {
        if (MinMax = -1)  ; æœ€å°åŒ–æ—¶ä¸å¤„ç†
            return

        ; è°ƒæ•´æ§ä»¶å¤§å°å’Œä½ç½®
        newWidth := Width - 20
        newEditHeight := Height - 100
        newButtonY := Height - 40

        infoCtrl.Move(, , newWidth)
        editCtrl.Move(, , newWidth, newEditHeight)

        ; é‡æ–°å®šä½æŒ‰é’®
        saveBtn.Move(, newButtonY)
        resetBtn.Move(, newButtonY)
        closeBtn.Move(newWidth - 100, newButtonY)
    }
}

; ç”Ÿæˆé…ç½®æ–‡æœ¬ï¼ˆINIæ ¼å¼ï¼‰
GenerateConfigText() {
    configText := "; " . Lang.SCRIPT_FULLNAME . " Configuration File`n"
    configText .= "; " . Lang.CONFIG_FORMAT_DESC . "`n"
    configText .= "; " . Lang.CONFIG_SECTION_DESC . "`n"
    configText .= "; " . Lang.CONFIG_OPTIONS_DESC . "`n"
    configText .= "; " . Lang.CONFIG_PATHS_DESC . "`n`n"

    sortedApps := GetSortedApps(true)  ; åŒ…å«ç¦ç”¨çš„åº”ç”¨
    for index, appData in sortedApps {
        appName := appData.key
        appConfig := appData.config

        configText .= "[" . appName . "]`n"

        ; åŸºæœ¬é…ç½®
        configText .= "hotkey=" . appConfig.hotkey . "`n"
        configText .= "exe=" . appConfig.exe . "`n"
        configText .= "launchCmd=" . appConfig.launchCmd . "`n"
        configText .= "name=" . appConfig.name . "`n"

        ; å¯åŠ¨è·¯å¾„ï¼ˆè½¬æ¢ä¸º | åˆ†éš”çš„å­—ç¬¦ä¸²ï¼‰
        if (appConfig.launchPaths.Length > 0) {
            pathStr := ""
            for i, path in appConfig.launchPaths {
                pathStr .= path
                if (i < appConfig.launchPaths.Length)
                    pathStr .= "|"
            }
            configText .= "launchPaths=" . pathStr . "`n"
        }

        ; å¯é€‰é…ç½®
        if (appConfig.HasOwnProp("maximize"))
            configText .= "maximize=" . (appConfig.maximize ? "true" : "false") . "`n"
        if (appConfig.HasOwnProp("cycleContinuous"))
            configText .= "cycleContinuous=" . (appConfig.cycleContinuous ? "true" : "false") . "`n"
        if (appConfig.HasOwnProp("disable"))
            configText .= "disable=" . (appConfig.disable ? "true" : "false") . "`n"

        configText .= "`n"
    }

    return configText
}

; ä»ç¼–è¾‘å™¨ä¿å­˜é…ç½®
SaveConfigFromEditor(editCtrl, configGui) {
    configText := editCtrl.Text

    ; è·å–é…ç½®æ–‡ä»¶è·¯å¾„
    configPath := GetConfigFilePath()

    try {
        ; ä¿å­˜åˆ°æ–‡ä»¶
        FileDelete(configPath)  ; åˆ é™¤æ—§æ–‡ä»¶
        FileAppend(configText, configPath, "UTF-8")

        ; é‡æ–°åŠ è½½é…ç½®
        if (LoadConfig(configPath)) {
            ; é‡æ–°æ³¨å†Œçƒ­é”®
            RegisterHotkeys()

            ; é‡å»ºæ‰˜ç›˜èœå•
            CreateTrayMenu()

            ; æ˜¾ç¤ºæˆåŠŸæ¶ˆæ¯
            MsgBox(Lang.CONFIG_SAVE_SUCCESS, Lang.CONFIG_TITLE . " - " . SCRIPT_NAME, "T3")

            ; å…³é—­ç¼–è¾‘å™¨çª—å£
            configGui.Destroy()
        } else {
            ; é…ç½®éªŒè¯å¤±è´¥
            MsgBox(Lang.CONFIG_VALIDATION_FAILED, Lang.CONFIG_TITLE . " - " . SCRIPT_NAME, 0x10)
        }
    } catch as e {
        ; ä¿å­˜å¤±è´¥
        MsgBox(Lang.CONFIG_SAVE_FAILED . " " . e.message, Lang.CONFIG_TITLE . " - " . SCRIPT_NAME, 0x10)
    }
}

; é‡ç½®é…ç½®ç¼–è¾‘å™¨å†…å®¹
ResetConfigEditor(editCtrl) {
    result := MsgBox(Lang.CONFIG_RESET_CONFIRM, Lang.CONFIG_TITLE . " - " . SCRIPT_NAME, 4)
    if (result = "Yes") {
        editCtrl.Text := GenerateConfigText()
    }
}

; æ˜¾ç¤ºå¯æ»šåŠ¨æ–‡æœ¬çª—å£ï¼ˆä¿ç•™ç”¨äºå…¶ä»–åŠŸèƒ½ï¼‰
ShowScrollableText(text, title := "Information", width := 600, height := 400) {
    ; åˆ›å»ºGUIçª—å£
    configGui := Gui("+Resize +MinSize400x300", title)
    configGui.MarginX := 10
    configGui.MarginY := 10

    ; æ·»åŠ ç¼–è¾‘æ§ä»¶ï¼ˆåªè¯»ï¼Œæ”¯æŒæ»šåŠ¨ï¼‰
    editCtrl := configGui.Add("Edit", "ReadOnly VScroll HScroll w" . (width - 40) . " h" . (height - 80), text)
    editCtrl.SetFont("s9", "Consolas")  ; ä½¿ç”¨ç­‰å®½å­—ä½“ä¾¿äºé˜…è¯»

    ; æ·»åŠ å…³é—­æŒ‰é’®
    closeBtn := configGui.Add("Button", "w100 h30", Lang.MENU_EXIT)
    closeBtn.OnEvent("Click", (*) => configGui.Destroy())

    ; è®¾ç½®çª—å£äº‹ä»¶
    configGui.OnEvent("Close", (*) => configGui.Destroy())
    configGui.OnEvent("Escape", (*) => configGui.Destroy())

    ; å±…ä¸­æ˜¾ç¤ºçª—å£
    configGui.Show("w" . width . " h" . height)

    ; çª—å£å¤§å°è°ƒæ•´äº‹ä»¶
    configGui.OnEvent("Size", ConfigGui_Size)

    ConfigGui_Size(GuiObj, MinMax, Width, Height) {
        if (MinMax = -1)  ; æœ€å°åŒ–æ—¶ä¸å¤„ç†
            return

        ; è°ƒæ•´ç¼–è¾‘æ§ä»¶å¤§å°
        editCtrl.Move(, , Width - 40, Height - 80)
        ; è°ƒæ•´æŒ‰é’®ä½ç½®
        closeBtn.Move((Width - 100) / 2, Height - 50)
    }
}

IsDisabled(appConfig) {
    if !appConfig.HasOwnProp("disable")
        return false

    ; å¦‚æœ disable æ˜¯å¸ƒå°”å€¼ï¼Œç›´æ¥è¿”å›
    if (Type(appConfig.disable) = "Integer")
        return appConfig.disable

    ; å¦‚æœæ˜¯å­—ç¬¦ä¸²ï¼Œè¿›è¡Œè½¬æ¢
    return ConvertToBool(appConfig.disable)
}

; è·å–æŒ‰å¿«æ·é”®æ’åºçš„åº”ç”¨åˆ—è¡¨
GetSortedApps(includeDisabled := false) {
    sortedApps := []

    ; æ”¶é›†æ‰€æœ‰åº”ç”¨
    for appKey, appConfig in Apps {
        if (!includeDisabled && IsDisabled(appConfig)) {
            continue
        }
        sortedApps.Push({ key: appKey, config: appConfig })
    }

    ; æŒ‰å¿«æ·é”®æ’åº
    if (sortedApps.Length > 1) {
        loop sortedApps.Length - 1 {
            outer := A_Index
            loop sortedApps.Length - outer {
                inner := A_Index
                ; ä½¿ç”¨å­—ç¬¦ä¸²æ¯”è¾ƒ
                hotkey1 := sortedApps[inner].config.hotkey
                hotkey2 := sortedApps[inner + 1].config.hotkey
                if (StrCompare(hotkey1, hotkey2) > 0) {
                    temp := sortedApps[inner]
                    sortedApps[inner] := sortedApps[inner + 1]
                    sortedApps[inner + 1] := temp
                }
            }
        }
    }

    return sortedApps
}


configFile := GetConfigFilePath()
if (FileExist(configFile)) {
    if (LoadConfig(configFile)) {
        ShowNotification(Lang.CONFIG_SUCCESS . ": " . configFile)
    } else {
        ShowNotification(Lang.CONFIG_FAILED . ": " . configFile)
        ; ShowCurrentConfig()
        ExitApp()
    }
} else {
    ShowNotification(Lang.CONFIG_NOT_FOUND . ": " . configFile)
}

; ShowCurrentConfig()


; ==================== å…¨å±€çŠ¶æ€è·Ÿè¸ª ====================
; è®°å½•æ¯ä¸ªåº”ç”¨çš„å½“å‰æ´»åŠ¨çª—å£ç´¢å¼•
AppWindowIndex := Map()

; ==================== è‡ªåŠ¨åŒ–çƒ­é”®æ³¨å†Œ ====================
; ğŸ¤– è¿™éƒ¨åˆ†ä¼šè‡ªåŠ¨æ ¹æ®é…ç½®åˆ›å»ºçƒ­é”®ï¼Œæ— éœ€æ‰‹åŠ¨ä¿®æ”¹ï¼

; å­˜å‚¨å·²æ³¨å†Œçš„çƒ­é”®
RegisteredHotkeys := Map()

; æ ¹æ®é…ç½®è‡ªåŠ¨æ³¨å†Œçƒ­é”®
RegisterHotkeys()

RegisterHotkeys() {
    ; å…ˆæ¸…é™¤æ‰€æœ‰å·²æ³¨å†Œçš„çƒ­é”®
    ClearAllHotkeys()

    for appKey, appConfig in Apps {
        if (IsDisabled(appConfig)) {
            continue
        }

        ; åˆå§‹åŒ–çª—å£ç´¢å¼•
        AppWindowIndex[appKey] := 0

        ; æ³¨å†Œä¸»çƒ­é”®ï¼ˆå¦‚ F3ï¼‰
        if (appConfig.HasOwnProp("hotkey") && appConfig.hotkey != "") {
            try
            {
                Hotkey(appConfig.hotkey, ToggleApp.Bind(appKey))
                RegisteredHotkeys[appConfig.hotkey] := appKey
            }
            catch as err {
                ShowNotification(Lang.HOTKEY_REGISTER_FAILED . ": " . appConfig.hotkey . " - " . err.message)
            }
        }
    }
}

; æ¸…é™¤æ‰€æœ‰å·²æ³¨å†Œçš„çƒ­é”®
ClearAllHotkeys() {
    for hotkey, appKey in RegisteredHotkeys {
        try {
            Hotkey(hotkey, "Off")
        } catch {
            ; å¿½ç•¥æ¸…é™¤å¤±è´¥çš„çƒ­é”®
        }
    }
    RegisteredHotkeys.Clear()
}

; ==================== æ ¸å¿ƒå‡½æ•° ====================

; è·å–åº”ç”¨çš„æ‰€æœ‰çª—å£å¥æŸ„
GetAppWindows(exe) {
    windows := []

    ; ä½¿ç”¨ WinGetList è·å–æ‰€æœ‰åŒ¹é…çš„çª—å£
    try {
        winList := WinGetList("ahk_exe " . exe)

        ; è¿‡æ»¤å‡ºå¯è§çš„ã€æœ‰æ ‡é¢˜çš„çª—å£
        for hwnd in winList {
            if (WinExist("ahk_id " . hwnd)) {
                ; æ£€æŸ¥çª—å£æ˜¯å¦å¯è§ä¸”æœ‰æ ‡é¢˜
                title := WinGetTitle("ahk_id " . hwnd)
                if (title != "" && IsWindow(hwnd)) {
                    windows.Push(hwnd)
                }
            }
        }

        ; æŒ‰çª—å£åˆ›å»ºæ—¶é—´æ’åºï¼Œç¡®ä¿é¡ºåºä¸€è‡´
        ; è¿™æ ·å¯ä»¥é¿å…çª—å£é¡ºåºå˜åŒ–å¯¼è‡´çš„é—®é¢˜
        if (windows.Length > 1) {
            ; ç®€å•çš„å†’æ³¡æ’åºï¼ŒæŒ‰çª—å£å¥æŸ„æ’åºï¼ˆé€šå¸¸åæ˜ åˆ›å»ºé¡ºåºï¼‰
            loop windows.Length - 1 {
                outer := A_Index
                loop windows.Length - outer {
                    inner := A_Index
                    if (windows[inner] > windows[inner + 1]) {
                        temp := windows[inner]
                        windows[inner] := windows[inner + 1]
                        windows[inner + 1] := temp
                    }
                }
            }
        }
    }
    catch {
        ; å¦‚æœå‡ºé”™ï¼Œè¿”å›ç©ºæ•°ç»„
    }

    return windows
}

; æ£€æŸ¥æ˜¯å¦ä¸ºæœ‰æ•ˆçª—å£
IsWindow(hwnd) {
    try {
        ; æ£€æŸ¥çª—å£æ˜¯å¦å¯è§
        return DllCall("IsWindowVisible", "Ptr", hwnd)
    }
    catch {
        return false
    }
}

; åˆ‡æ¢åº”ç”¨ç¨‹åºæ˜¾ç¤º/éšè—ï¼ˆæ”¯æŒå¤šçª—å£å¾ªç¯ï¼‰
ToggleApp(appKey, *) {
    if (!Apps.Has(appKey)) {
        ShowNotification(Lang.APP_NOT_FOUND . ": " . appKey)
        return
    }

    appConfig := Apps[appKey]
    windows := GetAppWindows(appConfig.exe)

    ; å¦‚æœæ²¡æœ‰çª—å£ï¼Œå¯åŠ¨åº”ç”¨
    if (windows.Length = 0) {
        LaunchApp(appKey)
        return
    }

    ; å¦‚æœåªæœ‰ä¸€ä¸ªçª—å£ï¼Œä½¿ç”¨åŸæ¥çš„é€»è¾‘
    if (windows.Length = 1) {
        hwnd := windows[1]
        isMinimized := WinGetMinMax(hwnd)

        if (WinActive(hwnd)) {
            ; å½“å‰æ´»åŠ¨çª—å£ - æœ€å°åŒ–
            WinMinimize(hwnd)
            ShowNotification(appConfig.name . " " . Lang.APP_MINIMIZED)
            AppWindowIndex[appKey] := 0  ; é‡ç½®ç´¢å¼•
        }
        else {
            ; çª—å£å­˜åœ¨ä½†ä¸æ´»åŠ¨ - æ¿€æ´»
            if (isMinimized = -1) {
                WinRestore(hwnd)
            }
            WinActivate(hwnd)
            WinShow(hwnd)
            ShowNotification(appConfig.name . " " . Lang.APP_ACTIVATED)
            AppWindowIndex[appKey] := 1
        }
        return
    }

    ; å¤šçª—å£å¤„ç†é€»è¾‘
    HandleMultipleWindows(appKey, windows, appConfig)
}

; å¤„ç†å¤šçª—å£å¾ªç¯æ¿€æ´»
HandleMultipleWindows(appKey, windows, appConfig) {
    currentIndex := AppWindowIndex[appKey]
    windowCount := windows.Length
    nextIndex := 0

    ; æ£€æŸ¥å½“å‰æ˜¯å¦æœ‰æ´»åŠ¨çš„åº”ç”¨çª—å£
    activeWindowIndex := 0
    for i, hwnd in windows {
        if (WinActive(hwnd)) {
            activeWindowIndex := i
            break
        }
    }

    ; ç¡®å®šä¸‹ä¸€ä¸ªçª—å£ç´¢å¼•
    if (activeWindowIndex > 0) {
        ; åŸºäºå½“å‰æ´»åŠ¨çª—å£è®¡ç®—ä¸‹ä¸€ä¸ªçª—å£
        nextIndex := activeWindowIndex + 1
    } else {
        ; æ²¡æœ‰æ£€æµ‹åˆ°æ´»åŠ¨çª—å£ï¼ŒåŸºäºä¿å­˜çš„ç´¢å¼•è®¡ç®—
        if (currentIndex <= 0 || currentIndex > windowCount) {
            ; ç´¢å¼•æ— æ•ˆï¼Œä»ç¬¬ä¸€ä¸ªçª—å£å¼€å§‹
            nextIndex := 1
        } else {
            ; åŸºäºä¿å­˜çš„ç´¢å¼•è®¡ç®—ä¸‹ä¸€ä¸ªçª—å£
            nextIndex := currentIndex + 1
        }
    }

    ; å¤„ç†å¾ªç¯è¾¹ç•Œ
    if (nextIndex > windowCount) {
        ; æ£€æŸ¥æ˜¯å¦å¯ç”¨äº†æŒç»­å¾ªç¯æ¨¡å¼
        if (appConfig.HasOwnProp("cycleContinuous") && appConfig.cycleContinuous) {
            ; æŒç»­å¾ªç¯æ¨¡å¼ï¼šå›åˆ°ç¬¬ä¸€ä¸ªçª—å£
            nextIndex := 1
        } else {
            ; é»˜è®¤æ¨¡å¼ï¼šæœ€å°åŒ–æ‰€æœ‰çª—å£
            for hwnd in windows {
                if (WinGetMinMax(hwnd) != -1) {  ; å¦‚æœçª—å£æ²¡æœ‰æœ€å°åŒ–
                    WinMinimize(hwnd)
                }
            }
            ShowNotification(appConfig.name . " " . Lang.APP_ALL_MINIMIZED . " (total " . windowCount . ")")
            AppWindowIndex[appKey] := 0  ; é‡ç½®ç´¢å¼•
            return
        }
    }

    ; æ¿€æ´»ç›®æ ‡çª—å£
    targetHwnd := windows[nextIndex]

    if (WinGetMinMax(targetHwnd) = -1) {  ; å¦‚æœçª—å£æœ€å°åŒ–äº†
        WinRestore(targetHwnd)
    }
    WinActivate(targetHwnd)
    WinShow(targetHwnd)

    ; æ›´æ–°ç´¢å¼•
    AppWindowIndex[appKey] := nextIndex

    ; ç”Ÿæˆçª—å£æ ‡é¢˜ä¿¡æ¯
    windowTitle := WinGetTitle(targetHwnd)
    if (StrLen(windowTitle) > 50) {
        windowTitle := SubStr(windowTitle, 1, 50) . "..."
    }

    ; æ·»åŠ å¾ªç¯æ ‡è®°
    cycleInfo := ""
    if (nextIndex = 1 && (activeWindowIndex = windowCount || currentIndex = windowCount)) {
        cycleInfo := " (cycle)"
    }

    ShowNotification(appConfig.name . " window " . nextIndex . "/" . windowCount . cycleInfo . ": " . windowTitle)
}

; å¤‡ç”¨æœ€å¤§åŒ–æ–¹æ¡ˆï¼ˆç”¨äºä¸æ”¯æŒå¯åŠ¨æ—¶æœ€å¤§åŒ–çš„åº”ç”¨ï¼‰
BackupMaximizeWindow(appKey) {
    if (!Apps.Has(appKey)) {
        return
    }

    appConfig := Apps[appKey]
    windows := GetAppWindows(appConfig.exe)

    if (windows.Length > 0) {
        firstWindow := windows[1]
        try {
            ; æ£€æŸ¥çª—å£æ˜¯å¦å·²ç»æ˜¯æœ€å¤§åŒ–çŠ¶æ€
            currentState := WinGetMinMax(firstWindow)
            windowTitle := WinGetTitle(firstWindow)

            if (currentState != 1) {  ; å¦‚æœä¸æ˜¯æœ€å¤§åŒ–çŠ¶æ€
                WinActivate(firstWindow)
                Sleep(100)
                WinMaximize(firstWindow)

                ; éªŒè¯æœ€å¤§åŒ–æ˜¯å¦æˆåŠŸ
                Sleep(100)
                newState := WinGetMinMax(firstWindow)
                if (newState = 1) {
                    ShowNotification("âœ“ " . appConfig.name . " " . Lang.APP_MAXIMIZED_BACKUP)
                } else {
                    ShowNotification("âš  " . appConfig.name . " " . Lang.APP_MAXIMIZED_FAILED)
                }
            } else {
                ; å·²ç»æ˜¯æœ€å¤§åŒ–çŠ¶æ€
                ShowNotification("âœ“ " . appConfig.name . " " . Lang.APP_ALREADY_MAXIMIZED)
            }
        }
        catch as e {
            ; é™é»˜å¤±è´¥ï¼Œä¸æ˜¾ç¤ºé”™è¯¯ä¿¡æ¯
        }
    }
}

; å¯åŠ¨åº”ç”¨ç¨‹åº
LaunchApp(appKey, *) {
    if (!Apps.Has(appKey)) {
        ShowNotification(Lang.APP_NOT_FOUND . ": " . appKey)
        return
    }

    appConfig := Apps[appKey]
    launched := false

    ; å°è¯•å„ç§å¯åŠ¨è·¯å¾„
    for path in appConfig.launchPaths {
        try
        {
            ; å¦‚æœè®¾ç½®äº†è‡ªåŠ¨æœ€å¤§åŒ–ï¼Œç›´æ¥ä»¥æœ€å¤§åŒ–çŠ¶æ€å¯åŠ¨
            if (appConfig.HasOwnProp("maximize") && appConfig.maximize) {
                Run(path, , "Max")  ; ç¬¬ä¸‰ä¸ªå‚æ•° "Max" è¡¨ç¤ºæœ€å¤§åŒ–å¯åŠ¨
                ShowNotification(Lang.APP_STARTING . " " . appConfig.name . " (" . Lang.APP_MAXIMIZED . ")...")

                ; æ·»åŠ å¤‡ç”¨æ–¹æ¡ˆï¼šå¦‚æœåº”ç”¨ä¸æ”¯æŒå¯åŠ¨æ—¶æœ€å¤§åŒ–ï¼Œ2ç§’åå°è¯•æ‰‹åŠ¨æœ€å¤§åŒ–
                SetTimer(() => BackupMaximizeWindow(appKey), -2000)
            } else {
                Run(path)
                ShowNotification(Lang.APP_STARTING . " " . appConfig.name . "...")
            }

            launched := true
            AppWindowIndex[appKey] := 0  ; Reset index
            break
        }
        catch {
            continue
        }
    }

    if (!launched) {
        MsgBox(Lang.LAUNCH_FAILED_MSG . " " . appConfig.name, Lang.LAUNCH_FAILED . " - " . SCRIPT_NAME, 0x10)
    }
}

; æ˜¾ç¤ºé€šçŸ¥
ShowNotification(message, delay := 500) {
    ToolTip(message)
    SetTimer(() => ToolTip(), -delay)
}

; ==================== æ‰©å±•åŠŸèƒ½ ====================

; æ˜¾ç¤ºåº”ç”¨çŠ¶æ€
ShowAppStatus() {
    status := Lang.STATUS_HEADER

    sortedApps := GetSortedApps(true)  ; åŒ…å«ç¦ç”¨çš„åº”ç”¨
    for index, appData in sortedApps {
        appKey := appData.key
        appConfig := appData.config

        windows := GetAppWindows(appConfig.exe)
        windowCount := windows.Length

        if (windowCount > 0) {
            activeCount := 0
            minimizedCount := 0

            for hwnd in windows {
                if (WinActive(hwnd)) {
                    activeCount++
                }
                else if (WinGetMinMax(hwnd) = -1) {
                    minimizedCount++
                }
            }

            statusText := windowCount . " " . Lang.STATUS_WINDOWS
            if (activeCount > 0) {
                statusText .= " (" . Lang.STATUS_ACTIVE . ": " . activeCount . ")"
            }
            if (minimizedCount > 0) {
                statusText .= " (" . Lang.STATUS_MINIMIZED . ": " . minimizedCount . ")"
            }

            status .= appConfig.hotkey . " - " . appConfig.name . ": " . statusText . "`n"
        }
        else {
            status .= appConfig.hotkey . " - " . appConfig.name . ": " . Lang.STATUS_NOT_RUNNING . "`n"
        }
    }

    MsgBox(status, Lang.STATUS_TITLE . " - " . SCRIPT_NAME, 0x1000)
}

; ==================== ç³»ç»Ÿæ‰˜ç›˜èœå• ====================

; ğŸ¤– è‡ªåŠ¨æ ¹æ®é…ç½®åˆ›å»ºæ‰˜ç›˜èœå•
CreateTrayMenu()

CreateTrayMenu() {
    ; æ¸…é™¤é»˜è®¤èœå•å¹¶åˆ›å»ºè‡ªå®šä¹‰èœå•
    A_TrayMenu.Delete()

    ; ä¸ºæ¯ä¸ªåº”ç”¨æ·»åŠ èœå•é¡¹ï¼ˆæŒ‰å¿«æ·é”®æ’åºï¼‰
    sortedApps := GetSortedApps(false)  ; ä¸åŒ…å«ç¦ç”¨çš„åº”ç”¨
    for index, appData in sortedApps {
        appKey := appData.key
        appConfig := appData.config
        A_TrayMenu.Add(Lang.MENU_TOGGLE . " " . appConfig.name . " (" . appConfig.hotkey . ")", ToggleApp.Bind(appKey))
    }

    A_TrayMenu.Add()  ; åˆ†éš”çº¿
    A_TrayMenu.Add(Lang.MENU_SHOW_STATUS, (*) => ShowAppStatus())
    A_TrayMenu.Add(Lang.MENU_SHOW_CONFIG, (*) => ShowCurrentConfig())
    A_TrayMenu.Add(Lang.MENU_OPEN_DIR, (*) => OpenScriptDirectory())
    A_TrayMenu.Add(Lang.MENU_OPEN_CONFIG, (*) => OpenConfigFile())
    A_TrayMenu.Add(Lang.MENU_HELP, ShowHelp)
    A_TrayMenu.Add()  ; åˆ†éš”çº¿
    A_TrayMenu.Add(Lang.MENU_EXIT, (*) => ExitApp())

    ; è®¾ç½®é»˜è®¤èœå•é¡¹
    A_TrayMenu.Default := Lang.MENU_SHOW_STATUS
}

; ==================== å¸®åŠ©å’Œç®¡ç† ====================

; æ˜¾ç¤ºå¸®åŠ©
ShowHelp(*) {
    ; åŠ¨æ€ç”Ÿæˆå¸®åŠ©æ–‡æœ¬
    helpText := Lang.SCRIPT_FULLNAME . Lang.HELP_CURRENT_APPS

    ; åˆ—å‡ºæ‰€æœ‰é…ç½®çš„åº”ç”¨ï¼ˆæŒ‰å¿«æ·é”®æ’åºï¼‰
    sortedApps := GetSortedApps(true)  ; åŒ…å«ç¦ç”¨çš„åº”ç”¨
    for index, appData in sortedApps {
        appConfig := appData.config
        helpText .= appConfig.hotkey . "  - " . appConfig.name . "`n"
    }

    helpText .= Lang.HELP_HOTKEY_DESC
    helpText .= Lang.HELP_FUNCTION_DESC
    helpText .= Lang.HELP_CYCLE_LOGIC
    helpText .= Lang.HELP_CONFIG_OPTIONS
    helpText .= Lang.HELP_ADD_APPS

    MsgBox(helpText, Lang.HELP_TITLE . " - " . SCRIPT_NAME)
}


; ==================== åˆå§‹åŒ– ====================

; ç”Ÿæˆå¯åŠ¨é€šçŸ¥
GenerateStartupNotification()

GenerateStartupNotification() {
    appList := ""
    sortedApps := GetSortedApps(false)  ; ä¸åŒ…å«ç¦ç”¨çš„åº”ç”¨
    for index, appData in sortedApps {
        appConfig := appData.config
        appList .= "- " . appConfig.hotkey . ":" . appConfig.name . "`n"
    }

    MsgBox(SCRIPT_NAME . " " . Lang.STARTUP_RUNNING . " `n`n" . appList, Lang.STARTUP_TITLE . " - " . SCRIPT_NAME, "T15")
}